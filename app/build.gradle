/*
 * Copyright (c) 2021 The Hydra authors
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

apply plugin: 'com.android.application'
apply plugin: 'be.ugent.zeus.hydra.licenses'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

// Read our properties, see bottom for details.
def props = loadProperties()

android {
    compileSdkVersion 30

    //noinspection GroovyMissingReturnStatement
    defaultConfig {
        applicationId "be.ugent.zeus.hydra"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 30200
        versionName "3.0.2"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // For a description of what these do, see the config.properties file.
        buildConfigField "boolean", "DEBUG_HOME_STREAM_PRIORITY", props.getProperty('hydra.debug.home.stream.priority')
        buildConfigField "boolean", "DEBUG_HOME_STREAM_STALL", props.getProperty('hydra.debug.home.stream.stall')
        buildConfigField "boolean", "DEBUG_HOME_STREAM_ADD_SKO_CARD", props.getProperty('hydra.debug.home.stream.sko')
        buildConfigField "boolean", "DEBUG_ENABLE_STRICT_MODE", props.getProperty('hydra.debug.strict_mode')
        buildConfigField "boolean", "DEBUG_ENABLE_ALL_SPECIALS", props.getProperty('hydra.debug.home.stream.specials')
        buildConfigField "boolean", "DEBUG_TRACK_LEAKS", props.getProperty("hydra.debug.leaks")
        buildConfigField "boolean", "DEBUG_ENABLE_REPORTING", props.getProperty("hydra.debug.reporting")

        // used by Room, to test migrations
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    lintOptions {
        warningsAsErrors true
        // We don't support Right to Left layouts.
        disable "RtlSymmetry",
                // We don't care about long vector paths.
                "VectorPath",
                // Http is still allowed for now.
                "InsecureBaseConfiguration",
                // Allow overdraw, mainly used for selectable backgrounds
                "Overdraw",
                // We use dependabot for dependencies
                "GradleDependency",
                // We decide when we upgrade
                "OldTargetApi"
    }

    if (props["signing"]) {
        signingConfigs {
            upload {
                keyAlias "upload"
                keyPassword props['keyPassword']
                storeFile file(props['storeFile'])
                storePassword props['storePassword']
                v1SigningEnabled true
                v2SigningEnabled true
            }
        }
    }

    flavorDimensions "distribution"

    productFlavors {
        // Play Store and officially supported version
        store {
            isDefault = true
            manifestPlaceholders = [
                    google_maps_key: props.getProperty('mapsApiKey'),
            ]
        }

        open {
            ext.enableCrashlytics = false
            versionNameSuffix "-open"
            applicationIdSuffix ".open"
            firebaseCrashlytics {
                mappingFileUploadEnabled false
            }
        }
    }

    // used by Room, to test migrations
    sourceSets {
        test.resources.srcDirs += files("$projectDir/schemas".toString())
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (props["signing"]) {
                signingConfig signingConfigs.upload
            }
        }

        debug {
            // Disable crashlytics in debug builds if necessary.
            ext.enableCrashlytics = Boolean.parseBoolean(props.getProperty("hydra.debug.reporting"))
            testCoverageEnabled true
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
        unitTests.all {
            systemProperty 'robolectric.logging.enabled', 'true'
        }
    }

    buildFeatures {
        viewBinding = true
    }

    //noinspection GroovyMissingReturnStatement
    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/notice.txt'
    }
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    implementation 'androidx.core:core:1.6.0'
    implementation 'androidx.media:media:1.4.1'
    implementation 'androidx.fragment:fragment:1.3.6'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.browser:browser:1.3.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation "androidx.lifecycle:lifecycle-common-java8:2.3.1"
    implementation 'androidx.room:room-runtime:2.3.0'
    annotationProcessor 'androidx.room:room-compiler:2.3.0'
    implementation 'com.squareup.okhttp3:okhttp:4.9.1'
    implementation 'com.squareup.moshi:moshi:1.12.0'
    implementation 'com.squareup.picasso:picasso:2.8'
    implementation 'com.github.cachapa:ExpandableLayout:2.9.2'
    implementation 'com.github.niknetniko:material-intro:9903deb6c4'
    implementation 'com.github.jonfinerty:Once:19fa6fa10d'
    implementation 'com.github.esnaultdev:MaterialValues:v1.1.1'

    // Dependencies for the Play Store version.
    storeImplementation 'com.google.android.gms:play-services-maps:17.0.1'
    storeImplementation 'com.google.firebase:firebase-analytics:19.0.1'
    storeImplementation 'com.google.firebase:firebase-crashlytics:18.2.0'

    // Dependencies for open version.
    openImplementation 'org.osmdroid:osmdroid-android:6.1.11'

    if (props.getProperty("hydra.debug.leaks").toBoolean()) {
        logger.info("Leak tracking enabled...")
        debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.7'
    }

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:3.12.4'
    testImplementation 'org.robolectric:robolectric:4.5.1'
    testImplementation 'androidx.test:core:1.4.0'
    testImplementation 'androidx.test.ext:junit:1.1.3'
    testImplementation 'androidx.test:rules:1.4.0'
    testImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    testImplementation 'androidx.test.espresso:espresso-intents:3.4.0'
    testImplementation 'androidx.test.espresso:espresso-contrib:3.4.0'
    testImplementation 'androidx.arch.core:core-testing:2.1.0'
    testImplementation 'androidx.room:room-testing:2.3.0'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.9.1'
    testImplementation 'nl.jqno.equalsverifier:equalsverifier:3.7.1'
    testImplementation 'com.github.niknetniko:GetterSetterVerifier:0.0.11'
    testImplementation 'com.shazam:shazamcrest:0.11'
    testImplementation 'org.skyscreamer:jsonassert:1.5.0'
    testImplementation 'org.jeasy:easy-random-core:5.0.0'
    testImplementation 'org.apache.commons:commons-lang3:3.12.0'
    testImplementation 'commons-validator:commons-validator:1.7'
}


// Disable Google services for open variant.
android.applicationVariants.all { variant ->
    def googleTask = tasks.findByName("process${variant.name.capitalize()}GoogleServices")
    googleTask.enabled = "open" != variant.flavorName
}

/**
 * Loads the default properties, and the user properties. This will also load the
 * secret keys.
 */
def loadProperties() {
    // Load the default properties.
    def defaultProps = new Properties()
    defaultProps.load(file("config.properties").newReader())

    // Load custom properties if available
    def customProps = new Properties(defaultProps)
    def customFile = file("custom-config.properties")
    if (customFile.exists()) {
        customProps.load(customFile.newReader())
    } else {
        logger.info('No custom-config.properties file was found.')
    }

    // Load the secret example secret keys.
    def exampleKeys = new Properties(customProps)
    exampleKeys.load(file('secrets.properties.example').newReader())

    // Load the actual keys if present
    def actualKeys = new Properties(exampleKeys)
    def actualKeyFile = file('secrets.properties')
    if (actualKeyFile.exists()) {
        actualKeys.load(actualKeyFile.newReader())
    } else {
        logger.warn('A secrets.properties file was not found.')
    }

    return actualKeys
}
