apply plugin: 'com.android.application'
//Enable this when disabling jack
//apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.cookpad.android.licensetools'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"

    defaultConfig {
        applicationId "be.ugent.zeus.hydra"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 210
        versionName "2.1.0"
        vectorDrawables.useSupportLibrary = true
        
        //Enable jack support
        //When disabling jack, DON'T forget to enable retrolambda
        jackOptions {
            enabled true
        }

        //Load the API keys from the file "secrets.properties".
        Properties props = new Properties()
        props.load(new FileInputStream(file('secrets.properties')))
        buildConfigField "String", "OAUTH_ID", "\"" + props['OAUTH_ID'] + "\""
        buildConfigField "String", "OAUTH_SECRET", "\"" + props['OAUTH_SECRET'] + "\""
        manifestPlaceholders = [ google_maps_key: props['MAPS_API_KEY'] ]
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
        }
        debug {
            // Set this to false on release versions!
            debuggable true
        }
    }

    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/notice.txt'
    }
}

project.afterEvaluate {
    preBuild.dependsOn checkLicenses, generateLicensePage
}

//The support library version
final SUPPORT_LIBRARY_VERSION = '25.0.1'

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile "com.android.support:support-v4:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:support-v13:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:appcompat-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:cardview-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:support-v13:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:preference-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:recyclerview-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:gridlayout-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:design:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:customtabs:$SUPPORT_LIBRARY_VERSION"

    compile 'org.springframework.android:spring-android-core:1.0.1.RELEASE'
    compile 'org.springframework.android:spring-android-rest-template:1.0.1.RELEASE'
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.timehop.stickyheadersrecyclerview:library:0.4.3@aar'
    compile 'com.jakewharton.threetenabp:threetenabp:1.0.4'
    compile 'net.sourceforge.streamsupport:streamsupport:1.5.2'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.github.KyoSherlock:expandablelayout:master-SNAPSHOT'
    compile 'com.github.pluscubed:recycler-fast-scroll:0.3.2@aar'
    compile 'com.futuremind.recyclerfastscroll:fastscroll:0.2.4'
    compile 'com.heinrichreimersoftware:material-intro:1.6'
    compile 'su.j2e:rv-joiner:1.0.9'
    compile 'com.github.alexfu:SQLiteQueryBuilder:0.3.0'

    // Used for the OAuth stuff
    compile ('org.apache.oltu.oauth2:org.apache.oltu.oauth2.client:1.0.2') {
        exclude group: 'org.apache.oltu.oauth2'
    }

    compile 'com.google.android.gms:play-services-analytics:10.0.1'
    compile 'com.google.android.gms:play-services-maps:10.0.1'

    compile 'com.google.firebase:firebase-core:10.0.1'
    compile 'com.google.firebase:firebase-crash:10.0.1'
    compile 'com.google.firebase:firebase-messaging:10.0.1'
    compile 'com.google.firebase:firebase-config:10.0.1'

    //Test code
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.2.9'

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'

    //Enable the normal library for unit tests
    testCompile ('org.threeten:threetenbp:1.3.2'){
        exclude group:'com.jakewharton.threetenabp', module:'threetenabp'
    }
}

apply plugin: 'com.google.gms.google-services'