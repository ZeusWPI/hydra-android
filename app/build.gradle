apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.cookpad.android.licensetools'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "be.ugent.zeus.hydra"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 245
        versionName "2.4"
        vectorDrawables.useSupportLibrary = true

        //Load the API keys from the file "secrets.progperties".
        Properties props = new Properties()
        props.load(new FileInputStream(file('secrets.properties')))
        buildConfigField "String", "OAUTH_ID", "\"" + props['OAUTH_ID'] + "\""
        buildConfigField "String", "OAUTH_SECRET", "\"" + props['OAUTH_SECRET'] + "\""
        manifestPlaceholders = [ google_maps_key: props['MAPS_API_KEY'] ]
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
        }
        debug {
            // Set this to false on release versions!
            debuggable true
        }
    }

    //We don't want exceptions, but values. This enables using the Log class (and related).
    //TODO: maybe we should just move tests that depend on this to the android tests?
    testOptions {
        unitTests.returnDefaultValues = true
    }

    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/notice.txt'
    }
}

project.afterEvaluate {
    preBuild.dependsOn checkLicenses, generateLicensePage
}

//The support library version
final SUPPORT_LIBRARY_VERSION = '25.3.1'
final GMS_VERSION = '10.2.1'

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile "com.android.support:support-v4:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:support-v13:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:appcompat-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:cardview-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:support-v13:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:preference-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:recyclerview-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:design:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:customtabs:$SUPPORT_LIBRARY_VERSION"

    compile "android.arch.lifecycle:runtime:1.0.0-alpha1"
    compile "android.arch.lifecycle:extensions:1.0.0-alpha1"
    annotationProcessor "android.arch.lifecycle:compiler:1.0.0-alpha1"

    compile 'org.springframework.android:spring-android-core:1.0.1.RELEASE'
    compile 'org.springframework.android:spring-android-rest-template:1.0.1.RELEASE'
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.timehop.stickyheadersrecyclerview:library:0.4.3@aar'
    compile 'com.jakewharton.threetenabp:threetenabp:1.0.5'
    compile 'net.sourceforge.streamsupport:streamsupport:1.5.4'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'net.cachapa.expandablelayout:expandablelayout:2.8'
    compile 'com.github.pluscubed:recycler-fast-scroll:0.3.2@aar'
    compile 'com.heinrichreimersoftware:material-intro:1.6'
    compile 'su.j2e:rv-joiner:1.0.9'
    compile 'com.github.alexfu:SQLiteQueryBuilder:0.3.0'
    compile 'com.jonathanfinerty.once:once:1.2.1'
    compile 'com.karumi:dexter:3.1.0'

    // Used for the OAuth stuff
    compile ('org.apache.oltu.oauth2:org.apache.oltu.oauth2.client:1.0.2') {
        exclude group: 'org.apache.oltu.oauth2'
    }

    compile "com.google.android.gms:play-services-analytics:$GMS_VERSION"
    compile "com.google.android.gms:play-services-maps:$GMS_VERSION"

    compile "com.google.firebase:firebase-core:$GMS_VERSION"

    testCompile project(':firebasenoop')
    debugCompile project(':firebasenoop')
    releaseCompile "com.google.firebase:firebase-crash:$GMS_VERSION"

    compile "com.google.firebase:firebase-messaging:$GMS_VERSION"
    compile "com.google.firebase:firebase-config:$GMS_VERSION"

    //Test code
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.7.20'

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'

    //Enable the normal library for unit tests
    testCompile ('org.threeten:threetenbp:1.3.2'){
        exclude group:'com.jakewharton.threetenabp', module:'threetenabp'
    }

    testCompile 'nl.jqno.equalsverifier:equalsverifier:2.2.1'
    testCompile ('com.shazam:shazamcrest:0.11') {
        exclude group: 'com.google.code.gson', module: 'gson'
    }
    testCompile 'io.github.benas:random-beans:3.6.0'
    testCompile 'org.apache.commons:commons-lang3:3.5'
    testCompile 'org.hamcrest:hamcrest-core:1.3'
    testCompile 'com.googlecode:openbeans:1.0'
}

apply plugin: 'com.google.gms.google-services'