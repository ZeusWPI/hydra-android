apply plugin: 'com.android.application'
apply plugin: 'com.jaredsburrows.license'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        applicationId "be.ugent.zeus.hydra"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 2800
        versionName "2.8.0"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        def props = loadProperties()
        manifestPlaceholders = [
                google_maps_key: props.getProperty('MAPS_API_KEY'),
                disableAnalytics: false,
                enableCrashReporting: true
        ]
        // Minerva authentication keys
        buildConfigField "String", "OAUTH_ID", '"' + props.getProperty('OAUTH_ID') + '"'
        buildConfigField "String", "OAUTH_SECRET", '"' + props.getProperty('OAUTH_SECRET') + '"'
        // For a description of what these do, see the config.properties file.
        buildConfigField "boolean", "DEBUG_HOME_STREAM_PRIORITY", props.getProperty('hydra.debug.home.stream.priority')
        buildConfigField "boolean", "DEBUG_HOME_STREAM_STALL", props.getProperty('hydra.debug.home.stream.stall')
        buildConfigField "boolean", "DEBUG_HOME_STREAM_ADD_SKO_CARD", props.getProperty('hydra.debug.home.stream.sko')
        buildConfigField "boolean", "DEBUG_ENABLE_STRICT_MODE", props.getProperty('hydra.debug.strict_mode')
        buildConfigField "boolean", "DEBUG_ENABLE_ALL_SPECIALS", props.getProperty('hydra.debug.home.stream.specials')
        buildConfigField "boolean", "DEBUG_NETWORK_ENABLE_LOGS", props.getProperty('hydra.debug.network.logging')

        // used by Room, to test migrations
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    // used by Room, to test migrations
    sourceSets {
        test.resources.srcDirs += files("$projectDir/schemas".toString())
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
        }
        debug {
            // Set this to false on release versions!
            debuggable true
            // Disable analytics in debug builds
            manifestPlaceholders = [
                    disableAnalytics: true,
                    enableCrashReporting: false
            ]
            ext.enableCrashlytics = false
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
        unitTests.all {
            systemProperty 'robolectric.logging.enabled', 'true'
        }
    }

    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/notice.txt'
    }

    // TODO: fork the material-intro library and remove databinding.
    dataBinding {
        enabled = true
    }
}

licenseReport {
    generateJsonReport = false
}

// TODO: Run the library generate task automatically.
// Might become automatic in the future, see https://github.com/jaredsburrows/gradle-license-plugin/issues/38.

// The support library version
final SUPPORT_LIBRARY_VERSION = '27.1.1'
final GMS_VERSION = '15.0.0'
final AAC_VERSION = '1.1.0'

dependencies {

    implementation project(':provider-contract')

    implementation "com.android.support:support-v4:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support:support-v13:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support:appcompat-v7:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support:cardview-v7:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support:support-v13:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support:preference-v7:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support:recyclerview-v7:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support:design:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support:customtabs:$SUPPORT_LIBRARY_VERSION"

    // Android Architecture Components
    implementation "android.arch.lifecycle:extensions:$AAC_VERSION"
    annotationProcessor "android.arch.lifecycle:compiler:$AAC_VERSION"
    testImplementation("android.arch.core:core-testing:$AAC_VERSION") {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-compat'
        exclude group: 'com.android.support', module: 'support-core-utils'
    }

    implementation "android.arch.persistence.room:runtime:1.0.0"
    annotationProcessor "android.arch.persistence.room:compiler:1.0.0"
    testImplementation("android.arch.persistence.room:testing:1.0.0") {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-compat'
        exclude group: 'com.android.support', module: 'support-core-utils'
    }

    // Mappers
    implementation 'org.mapstruct:mapstruct:1.2.0.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.2.0.Final'

    // Network stack
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'
    implementation 'com.squareup.moshi:moshi:1.5.0'

    implementation 'com.timehop.stickyheadersrecyclerview:library:0.4.3@aar'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.0.5'
    implementation 'net.sourceforge.streamsupport:streamsupport:1.6.0'
    implementation 'com.github.streamsupport.streamsupport:streamsupport-pro:-SNAPSHOT'
    implementation 'com.squareup.picasso:picasso:2.6.0-SNAPSHOT'
    implementation 'net.cachapa.expandablelayout:expandablelayout:2.9.2'
    implementation 'com.github.pluscubed:recycler-fast-scroll:0.3.2@aar'
    implementation 'com.heinrichreimersoftware:material-intro:1.6.2'
    implementation 'su.j2e:rv-joiner:1.0.9'
    implementation 'com.jonathanfinerty.once:once:1.2.2'
    implementation 'com.karumi:dexter:4.2.0'
    implementation 'com.github.captain-miao:optroundcardview:1.1.0'
    implementation 'blue.aodev:material-values:1.1.1'

    // Used for the OAuth stuff
    implementation('org.apache.oltu.oauth2:org.apache.oltu.oauth2.client:1.0.2') {
        exclude group: 'org.apache.oltu.oauth2'
    }

    implementation "com.google.android.gms:play-services-analytics:$GMS_VERSION"
    implementation "com.google.android.gms:play-services-maps:$GMS_VERSION"
    implementation "com.google.android.gms:play-services-auth:$GMS_VERSION"

    implementation "com.google.firebase:firebase-core:$GMS_VERSION"
    implementation "com.google.firebase:firebase-messaging:$GMS_VERSION"

    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.2'

    //Test code
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.13.0'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
    testImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'

    //Enable the normal library for unit tests
    testImplementation('org.threeten:threetenbp:1.3.6') {
        exclude group: 'com.jakewharton.threetenabp', module: 'threetenabp'
    }

    testImplementation 'com.squareup.okhttp3:mockwebserver:3.10.0'
    testImplementation 'nl.jqno.equalsverifier:equalsverifier:2.4.3'
    testImplementation 'com.shazam:shazamcrest:0.11'
    testImplementation 'org.skyscreamer:jsonassert:1.5.0'
    testImplementation 'io.github.benas:random-beans:3.7.0'
    testImplementation 'org.apache.commons:commons-lang3:3.7'
    testImplementation 'org.hamcrest:hamcrest-core:1.3'
    testImplementation 'org.robolectric:robolectric:3.8'
    testImplementation('org.springframework:spring-test:3.2.18.RELEASE') {
        exclude module: 'spring-core'
    }
    testImplementation 'commons-validator:commons-validator:1.6'
}

apply plugin: 'com.google.gms.google-services'

/**
 * Loads the default properties, and the user properties. This will also load the
 * secret keys.
 *
 * @return
 */
def loadProperties() {

    // Load the default properties.
    def defaultProps = new Properties()
    defaultProps.load(file("config.properties").newReader())

    // Load custom properties if available
    def customProps = new Properties(defaultProps)
    def customFile = file("custom-config.properties")
    if (customFile.exists()) {
        customProps.load(customFile.newReader())
    } else {
        logger.info('No custom-config.properties file was found.')
    }

    // Load the secret example secret keys.
    def exampleKeys = new Properties(customProps)
    exampleKeys.load(file('secrets.properties.example').newReader())

    // Load the actual keys if present
    def actualKeys = new Properties(exampleKeys)
    def actualKeyFile = file('secrets.properties')
    if (actualKeyFile.exists()) {
        actualKeys.load(actualKeyFile.newReader())
    } else {
        logger.warn("A secrets.properties file was not found. Minerva and other APIs will not work.")
    }

    return actualKeys
}