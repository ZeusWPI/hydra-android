apply plugin: 'com.android.library'

def versions = loadAndroidVersions()

android {
    // We need this for Nix flakes
    //noinspection GrDeprecatedAPIUsage
    buildToolsVersion versions.buildToolsVersions

    defaultConfig {
        compileSdk versions.platformVersions as Integer
        minSdk 21
        targetSdk 34
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    namespace 'com.heinrichreimersoftware.materialintro'

    lint {
        disable 'Overdraw', 'OldTargetApi', 'GradleDependency'
        showAll true
        warningsAsErrors true
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.material:material:1.10.0'
}

// TODO: extract this duplicate code...
/**
 * Loads the default properties, and the user properties. This will also load the
 * secret keys.
 */
def loadAndroidVersions() {
    def defaultProps = new Properties()
    defaultProps.load(file("../android-versions.toml").newReader())

    def strippedProps = new Properties()
    for (e in defaultProps) {
        strippedProps.setProperty(e.key, e.value.replaceAll('"', ''))
    }

    return strippedProps
}